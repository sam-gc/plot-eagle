import ../utils/stdlib.egl
import ../utils/misc.egl
import expressions.egl
export *

class Function
{
    double*     values
    int         vcount
    bool        discreet
    bool        ustatic
    Expression^ source

    init(Expression^ e, WindowInfo* winfo)
    {
        self.values = malloc(winfo.width * sizeof(double))
        self.vcount = winfo.width
        self.discreet = no
        self.ustatic  = no

        for var i = 0; i < winfo.width; i += 1
        {
            double x = i
            pmap(winfo, &x, nil)
            self.values[i] = e.eval(x)
        }

        self.source = e
    }

    destruct()
    {
        free(self.values)
    }

    func max() : double
    {
        double max = self.values[0]
        for var i = 1; i < self.vcount; i += 1
        {
            if self.values[i] > max
                max = self.values[i]
        }

        return max
    }

    func min() : double
    {
        double min = self.values[0]
        for var i = 1; i < self.vcount; i += 1
        {
            if self.values[i] < min
                min = self.values[i]
        }

        return min
    }
}

