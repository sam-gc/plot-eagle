import lexer.egl
import ../utils/math.egl
export *


interface Expression
{
    func eval(double) : double
    func render() : byte*
}

enum BuiltinFunction
{
    Invalid : 100 -- Start where Token ends
    Sin
    Cos
    Tan
    ArcSin
    ArcCos
    ArcTan
    Abs
    Rand

    TwoParam : 200 -- Start Two Parameter Functions
    LogB
}

class FunctionCall (Expression)
{
    Expression^     value1
    Expression^     value2
    BuiltinFunction f

    init(BuiltinFunction f, Expression^ value1, Expression^ value2)
    {
        self.value1 = value1
        self.value2 = value2
        self.f = f
    }

    func eval(double x) : double
    {
        double v = self.value1.eval(x)
        double v2

        if self.value2
            v2 = self.value2.eval(x)

        var f = self.f

        if   f == Sin;    return sin(v)
        elif f == Cos;    return cos(v)
        elif f == Tan;    return tan(v)
        elif f == Abs;    return fabs(v)
        elif f == ArcSin; return asin(v)
        elif f == ArcCos; return acos(v)
        elif f == ArcTan; return atan(v)
        elif f == LogB;   return log(v2) / log(v)
        return 0
    }

    func render() : byte*
    {
        return nil
    }
}

class UnaryOp (Expression)
{
    Expression^ value

    Token op

    init(Expression^ value, Token op)
    {
        self.value = value
        self.op = op
    }

    func eval(double x) : double
    {
        double v = self.value.eval(x)
        Token op = self.op

        if op == UMinus; return -v
        return 0
    }

    func render() : byte*
    {
        var v = self.value.render()
        byte b = 0
        var op = self.op
        if op == UMinus; b = `-`

        byte* me = malloc(strlen(v) + 10)
        sprintf(me, '%c%s', b, v)

        free(v)
        return me
    }
}

class BinaryOp (Expression)
{
    Expression^ left
    Expression^ right

    Token op

    init(Expression^ left, Expression^ right, Token op)
    {
        self->left = left
        self->right = right
        self->op = op
    }

    func eval(double x) : double
    {
        double l = self.left->eval(x)
        double r = self.right->eval(x)
        Token op = self.op

        if op == Plus; return l + r
        if op == Minus; return l - r
        if op == Mult; return l * r
        if op == Div; return l / r
        if op == Pow; return pow(l, r)

        return 0.0
    }

    func render() : byte*
    {
        var l = self.left.render()
        var r = self.right.render()

        byte b = 0
        var op = self.op
        if op == Plus; b = `+`
        if op == Minus; b = `-`
        if op == Mult; b = `*`
        if op == Div; b = `/`

        byte* me = malloc(strlen(l) + strlen(r) + 10)
        sprintf(me, '(%s %c %s)', l, b, r)

        free(l)
        free(r)
        return me
    }
}

class Value (Expression)
{
    double val
    bool   isvar

    init(double val)
    {
        self->val = val
        self->isvar = no
    }

    func eval(double x) : double
    {
        if self->isvar
            return x
        return self->val
    }

    func render() : byte*
    {
        byte* mem = malloc(20)
        sprintf(mem, '%lf', self->val)
        return mem
    }
}

func arg_count(BuiltinFunction f) : byte
{
    if f > TwoParam
        return 2
    return 1
}

