import stdio.egl

export *

extern func fabs(double) : double

struct WindowInfo
{
    double xmin; double xmax
    double ymin; double ymax

    double width; double height
}

func pprint(double d)
{
    if d != 0.0 && (fabs(d) > 1.0e9 || fabs(d) < 1.0e-4)
        printf('%.8e\n', d)
    else
        printf('%lf\n', d)
}

func pmap(WindowInfo* winfo, double* x, double* y)
{
    if x
    {
        var old = x^
        var perc = old / winfo^.width
        var span = (winfo^.xmax - winfo^.xmin) * perc
        x^ = winfo^.xmin + span
    }
    
    if y
    {
        var old = y^
        var perc = old / winfo^.height
        var span = (winfo^.ymax - winfo^.ymin) * perc
        y^ = winfo^.ymin + span
    }
}

func unmap(WindowInfo* winfo, double* x, double* y)
{
    if x
    {
        var old = x^
        var perc = (old - winfo^.xmin) / (winfo^.xmax - winfo^.xmin)
        x^ = winfo^.width * perc
    }

    if y
    {
        var old = y^
        var perc = (old - winfo^.ymin) / (winfo^.ymax - winfo^.ymin)
        y^ = winfo^.height - winfo^.height * perc
    }
}

func fill_winfo(WindowInfo* winfo, double width, double height)
{
    winfo^.xmin = -20
    winfo^.xmax = 20
    winfo^.ymin = -20
    winfo^.ymax = 20
    winfo^.height = height
    winfo^.width = width
}

gen window_x_coords(WindowInfo* winfo) : int
{
    for int i = 0; i < winfo^.width; i+= 1
    {
        yield i
    }
}

